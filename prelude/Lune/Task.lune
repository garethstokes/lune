module Lune.Task exposing (
  succeed,
  fail,
  fromIO,
  map,
  andThen,
  then,
  mapError,
  onError,
  attempt,
  result
)

{-| `Task e a` is the primary user-facing effect in Lune.

It represents an effectful computation that may fail with an error `e`
or succeed with a value `a`.
-}

import Lune.Prelude exposing (Task(..), Result(..))
import Lune.IO as IO

succeed : a -> Task e a
succeed a =
  Task (IO.pure (Ok a))

fail : e -> Task e a
fail e =
  Task (IO.pure (Err e))

fromIO : IO a -> Task e a
fromIO io =
  Task (IO.andThen io (\a -> IO.pure (Ok a)))

map : (a -> b) -> Task e a -> Task e b
map f task =
  case task of
    Task io ->
      Task
        ( IO.andThen io (\r ->
            case r of
              Err e -> IO.pure (Err e)
              Ok a -> IO.pure (Ok (f a))
          )
        )

andThen : Task e a -> (a -> Task e b) -> Task e b
andThen task k =
  case task of
    Task io ->
      Task
        ( IO.andThen io (\r ->
            case r of
              Err e -> IO.pure (Err e)
              Ok a ->
                case k a of
                  Task io2 -> io2
          )
        )

then : Task e a -> Task e b -> Task e b
then ta tb =
  andThen ta (\_ -> tb)

mapError : (e -> e2) -> Task e a -> Task e2 a
mapError f task =
  case task of
    Task io ->
      Task
        ( IO.andThen io (\r ->
            case r of
              Err e -> IO.pure (Err (f e))
              Ok a -> IO.pure (Ok a)
          )
        )

onError : Task e a -> (e -> Task e2 a) -> Task e2 a
onError task handler =
  case task of
    Task io ->
      Task
        ( IO.andThen io (\r ->
            case r of
              Ok a -> IO.pure (Ok a)
              Err e ->
                case handler e of
                  Task io2 -> io2
          )
        )

attempt : Task e a -> IO (Result e a)
attempt task =
  case task of
    Task io -> io

-- | Convert a Task's success/failure into a Result value.
-- The returned Task never fails (error type is unconstrained).
result : Task e a -> Task x (Result e a)
result task =
  onError (map Ok task) (\e -> succeed (Err e))
