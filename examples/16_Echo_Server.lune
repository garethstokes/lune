module EchoServer exposing (main)

import Lune.IO as IO
import Lune.Net.Socket as Socket
import Lune.String as Str
import Lune.Prelude exposing (Result(..))

main : IO Unit
main =
  do
    IO.println "Starting echo server on port 8080..."
    socketResult <- Socket.listen 8080
    case socketResult of
      Err _ ->
        IO.println "Failed to listen"
      Ok sock ->
        do
          IO.println "Waiting for connection..."
          connResult <- Socket.accept sock
          case connResult of
            Err _ ->
              IO.println "Accept failed"
            Ok conn ->
              do
                IO.println "Client connected!"
                handleClient conn
                _ <- Socket.closeConn conn
                _ <- Socket.closeSocket sock
                IO.println "Server shutdown"

handleClient : Socket.Connection -> IO Unit
handleClient conn =
  do
    recvResult <- Socket.recv conn
    case recvResult of
      Err _ ->
        IO.println "Recv error"
      Ok msg ->
        case Str.eq msg "" of
          True ->
            IO.println "Client disconnected"
          False ->
            do
              IO.println (Str.append "Received: " msg)
              _ <- Socket.send conn (Str.append "Echo: " msg)
              handleClient conn
